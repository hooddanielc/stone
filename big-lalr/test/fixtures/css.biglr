// TOKEN DIAGRAMS: HTTPS://WWW.W3.ORG/TR/CSS-SYNTAX-3/#TOKEN-DIAGRAMS
// PARSER DIAGRAMS: HTTPS://WWW.W3.ORG/TR/CSS-SYNTAX-3/#PARSER-DIAGRAMS
// CSS4 GRAMMAR: HTTPS://WWW.W3.ORG/TR/SELECTORS-4/#GRAMMAR

// '>' = GREATER_THAN
// '<' = LESSER_THEN
// '+' = PLUS
// '~' = TILDE
// '|' = PIPE
// '||' = COLUMN_COMBINATOR
// '*' = ASTERISK
// '$' = DOLLAR
// '^' = CARET
// '\t' = TAB
// '(' = LEFT_PARAN
// ')' = RIGHT_PARAN
// '@' = AT
// '%' = PERCENT
// '.' = PERIOD

// '/* */' = COMMENT
// '\n' | '\r\n' | '\r' | '\f' = NEWLINE
// '\s' | '\t' | NEWLINE = WHITESPACE
// '\n' | '\r\n' | '\r' | '\f' = NEWLINE
// 0-9 a-f or A-F = HEX_DIGIT
// '\' = ESCAPE
// '~=' = INCLUDE_MATCH_TOKEN
// '|=' = DASH_MATCH_TOKEN
// '^=' = PREFIX_MATCH_TOKEN
// '$=' = SUFFIX_MATCH_TOKEN
// '*=' = SUBSTRING_MATCH_TOKEN
// '||' = COLUMN_TOKEN
// '<!--' = CDO_TOKEN
// '-->' = CDC_TOKEN
// '[' = LEFT_BRACKET
// ']' = RIGHT_BRACKET
// 'i' = LETTER_I
// '=' = EQUALS
// '@' = AT

/token GREATER_THAN LESSER_THEN PLUS TILDE PIPE COLUMN_TOKEN ASTERISK DOLLAR CARET TAB LEFT_PAREN \
       RIGHT_PAREN COMMENT NEWLINE WHITESPACE IDENT_TOKEN HEX_DIGIT ESCAPE HASH_TOKEN STRING_TOKEN \
       NUMBER_TOKEN UNICODE_RANGE_TOKEN INCLUDE_MATCH_TOKEN DASH_MATCH_TOKEN PREFIX_MATCH_TOKEN \
       SUFFIX_MATCH_TOKEN SUBSTRING_MATCH_TOKEN CDO_TOKEN CDC_TOKEN COMMA LEFT_BRACKET \
       RIGHT_BRACKET LETTER_I EQUALS AT PERCENT COLON ANY_VALUE PERIOD

/top -> SELECTOR_LIST

WHITESPACE_TOKEN -> WHITESPACE
WHITESPACE_TOKEN -> WHITESPACE_TOKEN WHITESPACE
FUNCTION_TOKEN -> IDENT_TOKEN LEFT_PAREN
AT_KEYWORD_TOKEN -> AT IDENT_TOKEN
DIMENSION_TOKEN -> NUMBER_TOKEN IDENT_TOKEN
PERCENTAGE_TOKEN -> NUMBER_TOKEN PERCENT

// <selector-list> = <complex-selector-list>
SELECTOR_LIST -> COMPLEX_SELECTOR_LIST

// <complex-selector-list> = <complex-selector>#
COMPLEX_SELECTOR_LIST -> COMPLEX_SELECTOR
COMPLEX_SELECTOR_LIST -> COMPLEX_SELECTOR_LIST COMMA COMPLEX_SELECTOR

// <compound-selector-list> = <compound-selector>#
COMPOUND_SELECTOR_LIST -> COMPOUND_SELECTOR
COMPOUND_SELECTOR_LIST -> COMPOUND_SELECTOR_LIST COMMA COMPOUND_SELECTOR

// <simple-selector-list> = <simple-selector>#
SIMPLE_SELECTOR_LIST -> SIMPLE_SELECTOR
SIMPLE_SELECTOR_LIST -> SIMPLE_SELECTOR_LIST COMMA SIMPLE_SELECTOR

// <relative-selector-list> = <relative-selector>#
RELATIVE_SELECTOR_LIST -> RELATIVE_SELECTOR
RELATIVE_SELECTOR_LIST -> RELATIVE_SELECTOR_LIST COMMA RELATIVE_SELECTOR

// <complex-selector> = <compound-selector> [ <combinator>? <compound-selector> ]*
COMPLEX_SELECTOR -> COMPOUND_SELECTOR
COMPLEX_SELECTOR -> COMPLEX_SELECTOR COMBINATOR COMPOUND_SELECTOR
COMPLEX_SELECTOR -> COMPLEX_SELECTOR COMPOUND_SELECTOR

// <relative-selector> = <combinator>? <complex-selector>
RELATIVE_SELECTOR -> COMPLEX_SELECTOR
RELATIVE_SELECTOR -> COMBINATOR COMPLEX_SELECTOR

// <compound-selector> = [
//   <type-selector>? <subclass-selector>*
//   [ <pseudo-element-selector> <pseudo-class-selector>* ]*
// ]!
COMPOUND_SELECTOR_SUBCLASS_SELECTOR_LIST -> SUBCLASS_SELECTOR
COMPOUND_SELECTOR_SUBCLASS_SELECTOR_LIST -> COMPOUND_SELECTOR_SUBCLASS_SELECTOR_LIST SUBCLASS_SELECTOR
COMPOUND_SELECTOR_PSUEDO_CLASS_SELECTOR_LIST -> PSEUDO_CLASS_SELECTOR
COMPOUND_SELECTOR_PSUEDO_CLASS_SELECTOR_LIST -> COMPOUND_SELECTOR_PSUEDO_CLASS_SELECTOR_LIST PSEUDO_CLASS_SELECTOR
COMPOUND_SELECTOR_PSEUDO_SELECTOR_LIST -> PSEUDO_ELEMENT_SELECTOR
COMPOUND_SELECTOR_PSEUDO_SELECTOR_LIST -> PSEUDO_ELEMENT_SELECTOR COMPOUND_SELECTOR_PSUEDO_CLASS_SELECTOR_LIST
COMPOUND_SELECTOR_PSEUDO_SELECTOR_LIST -> PSEUDO_ELEMENT_SELECTOR COMPOUND_SELECTOR_PSEUDO_SELECTOR_LIST
COMPOUND_SELECTOR_PSEUDO_SELECTOR_LIST -> PSEUDO_ELEMENT_SELECTOR COMPOUND_SELECTOR_PSUEDO_CLASS_SELECTOR_LIST COMPOUND_SELECTOR_PSEUDO_SELECTOR_LIST
COMPOUND_SELECTOR -> TYPE_SELECTOR
COMPOUND_SELECTOR -> TYPE_SELECTOR COMPOUND_SELECTOR_SUBCLASS_SELECTOR_LIST
COMPOUND_SELECTOR -> TYPE_SELECTOR COMPOUND_SELECTOR_PSUEDO_CLASS_SELECTOR_LIST
COMPOUND_SELECTOR -> TYPE_SELECTOR COMPOUND_SELECTOR_SUBCLASS_SELECTOR_LIST COMPOUND_SELECTOR_PSUEDO_CLASS_SELECTOR_LIST
COMPOUND_SELECTOR -> COMPOUND_SELECTOR_SUBCLASS_SELECTOR_LIST COMPOUND_SELECTOR_PSUEDO_CLASS_SELECTOR_LIST
COMPOUND_SELECTOR -> COMPOUND_SELECTOR_PSEUDO_SELECTOR_LIST
COMPOUND_SELECTOR -> COMPOUND_SELECTOR_SUBCLASS_SELECTOR_LIST

// <simple-selector> = <type-selector> | <subclass-selector>
SIMPLE_SELECTOR -> TYPE_SELECTOR
SIMPLE_SELECTOR -> SUBCLASS_SELECTOR

// <combinator> = '>' | '+' | '~' | [ '||' ]
COMBINATOR -> GREATER_THAN
COMBINATOR -> PLUS
COMBINATOR -> TILDE
COMBINATOR -> COLUMN_TOKEN

// <type-selector> = <wq-name> | <ns-prefix>? '*'
TYPE_SELECTOR -> WQ_NAME
TYPE_SELECTOR -> ASTERISK
TYPE_SELECTOR -> NS_PREFIX ASTERISK

// <ns-prefix> = [ <ident-token> | '*' ]? '|'
NS_PREFIX -> PIPE
NS_PREFIX -> IDENT_TOKEN ASTERISK PIPE
NS_PREFIX -> IDENT_TOKEN PIPE
NS_PREFIX -> ASTERISK PIPE

// <wq-name> = <ns-prefix>? <ident-token>
WQ_NAME -> NS_PREFIX IDENT_TOKEN
WQ_NAME -> IDENT_TOKEN

// <subclass-selector> =
//   <id-selector> |
//   <class-selector> |
//   <attribute-selector> |
//   <pseudo-class-selector>
SUBCLASS_SELECTOR -> ID_SELECTOR
SUBCLASS_SELECTOR -> CLASS_SELECTOR
SUBCLASS_SELECTOR -> ATTRIBUTE_SELECTOR
SUBCLASS_SELECTOR -> PSEUDO_CLASS_SELECTOR

// <id-selector> = <hash-token>
ID_SELECTOR -> HASH_TOKEN

// <class-selector> = '.' <ident-token>
CLASS_SELECTOR -> PERIOD IDENT_TOKEN

// <attribute-selector> =
//   '[' <wq-name> ']' |
//   '[' <wq-name> <attr-matcher> [ <string-token> | <ident-token> ] <attr-modifier>? ']'
ATTRIBUTE_SELECTOR -> LEFT_BRACKET WQ_NAME RIGHT_BRACKET
ATTRIBUTE_SELECTOR -> LEFT_BRACKET WQ_NAME ATTR_MATCHER IDENT_TOKEN  ATTR_MODIFIER RIGHT_BRACKET
ATTRIBUTE_SELECTOR -> LEFT_BRACKET WQ_NAME ATTR_MATCHER STRING_TOKEN ATTR_MODIFIER RIGHT_BRACKET
ATTRIBUTE_SELECTOR -> LEFT_BRACKET WQ_NAME ATTR_MATCHER IDENT_TOKEN RIGHT_BRACKET
ATTRIBUTE_SELECTOR -> LEFT_BRACKET WQ_NAME ATTR_MATCHER STRING_TOKEN RIGHT_BRACKET

// <attr-matcher> = [ '~' | '|' | '^' | '$' | '*' ]? '='
ATTR_MATCHER -> ASTERISK EQUALS
ATTR_MATCHER -> DOLLAR EQUALS
ATTR_MATCHER -> CARET EQUALS
ATTR_MATCHER -> PIPE EQUALS
ATTR_MATCHER -> TILDE EQUALS
ATTR_MATCHER -> EQUALS

// <attr-modifier> = i
ATTR_MODIFIER -> LETTER_I

// <pseudo-class-selector> =
//  ':' <ident-token> |
//  ':' <function-token> <any-value> ')'
PSEUDO_CLASS_SELECTOR -> COLON IDENT_TOKEN
PSEUDO_CLASS_SELECTOR -> COLON FUNCTION_TOKEN ANY_VALUE RIGHT_PAREN
PSEUDO_ELEMENT_SELECTOR -> COLON PSEUDO_CLASS_SELECTOR
